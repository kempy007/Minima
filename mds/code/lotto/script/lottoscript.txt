/*
  LOTTO SCRIPT

  Funky little script.. with comments. Works as is in Script IDE. 
   
  0 - Round Number
   
  PLAYER 1 puts their game details
  1 - PLAYER 1 PUBKEY
  2 - PLAYER 1 HASH OF SECRET
  3 - PLAYER 1 REQUESTED ODDS
  4 - PLAYER 2 PUBKEY
  5 - PLAYER 2 PAYOUT ADDRESS
  6 - UID of GAME
  7 - FEES

  PLAYER 2 adds his details
  8 - RANDOM HASH VALUE
  
  Then player 1 reveals..
  9 - PLAYER 1 SECRET

*/

/* What state are we at */
LET round = STATE(0) 
LET prevround = PREVSTATE(0) 

/* Make sure we are 1 round ahead of before */
ASSERT round EQ INC(prevround) 

/* Define some useful vars */
LET playerpubkey = PREVSTATE(1)
LET secret = PREVSTATE(2)
LET odds = PREVSTATE(3)
LET lottopubkey  = PREVSTATE(4)
LET lottoaddress = PREVSTATE(5)
LET fees = PREVSTATE(7)

/* Check Odds are within range */
ASSERT (odds GT 0) AND (odds LT 1)

/* PLAYER 2 Joins OR Player 1 Cancels */
IF round EQ 1 THEN 

    /* Lotto has 20 blocks to proceed - or player can cancel */
    IF @COINAGE GT 20 AND SIGNEDBY(playerpubkey) THEN RETURN TRUE ENDIF 

    /* Check Signed by Lotto */
    ASSERT SIGNEDBY(lottopubkey)

    /* Make sure all the details are kept */
    ASSERT SAMESTATE (1 7)

    /* The Random HASH chosen by the LOTTO runner */
    LET lottorand = STATE(8)

    /* Check the rand number specified correctly */
    ASSERT ( (HEX(lottorand) EQ lottorand ) AND (LEN(lottorand) LTE 32) )

    /* The FEES */
    LET requiredfees = @AMOUNT * fees
    LET actualamount = @AMOUNT - requiredfees 

    /* Required payout to main game - fees */
    LET requiredamount = actualamount / odds

    /* Now check all the correct outputs are present */
    RETURN VERIFYOUT ( @INPUT @ADDRESS requiredamount @TOKENID TRUE )

/* PLAYER 1 REVEALS HIS HAND */
ELSEIF round EQ 2 THEN 

    /* Player has 20 blocks to claim - or Lotto can claim it all */
    IF @COINAGE GT 20 AND SIGNEDBY(lottopubkey) THEN RETURN TRUE ENDIF 

    /* Check Signed by Player */
    ASSERT SIGNEDBY(playerpubkey)

    /* Check the provided preimage is correct */
    LET preimage  = STATE(9)
    LET checkhash = SHA3(preimage)
    ASSERT checkhash EQ secret

    /* The Random HASH chosen by the LOTTO runner */
    LET lottorand = PREVSTATE(8)
    
   /* NOW hash the 2 value */
    LET decider = SHA3(CONCAT(preimage lottorand))

    /* Calculate the number value of the first 8 bytes */
    LET hexsubset = SUBSET(0 8 decider)
    LET numvalue = NUMBER(hexsubset)
    LET maxvalue = NUMBER(0xFFFFFFFFFFFFFFFF)
    LET target = FLOOR(maxvalue * odds)

    /* Check if a winner */
    LET iswin = numvalue LTE target
    IF iswin THEN 

        /* Player gets the money */ 
        RETURN TRUE
    ELSE
         
        /* Send to Lotto address */
        RETURN VERIFYOUT ( @INPUT lottoaddress @AMOUNT @TOKENID TRUE )
    ENDIF
ENDIF

